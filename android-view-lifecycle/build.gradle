
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}
android {
    compileSdk 33

    defaultConfig {
        minSdk 19
        targetSdk 33
        versionCode 6
        versionName "1.0.7"
    }

    buildTypes {
        release {
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

}

dependencies {

    // AndroidX
    implementation "androidx.core:core-ktx:1.10.0"
    implementation "androidx.fragment:fragment-ktx:1.6.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

File localPropsFile = rootProject.file('local.properties')
def localProps = new Properties()

if (localPropsFile.exists()) {
    localProps.load(new FileInputStream(localPropsFile))
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/digitalmatefr/AndroidViewLifecycle")
                credentials {
                    username = localProps.getProperty("github.username")
                    password = localProps.getProperty("github.token")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                groupId 'com.touticom'
                artifactId 'androidviewlifecycle'
                version android.defaultConfig.versionName
            }
        }
    }
}